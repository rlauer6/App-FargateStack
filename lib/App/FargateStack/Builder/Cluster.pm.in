package App::FargateStack::Builder::Cluster;

use strict;
use warnings;

use Carp;
use Data::Dumper;
use English qw(-no_match_vars);

use App::FargateStack::Constants;

use Role::Tiny;

########################################################################
sub build_fargate_cluster {
########################################################################
  my ($self) = @_;

  my $config = $self->get_config;

  my $dryrun = $self->get_dryrun;

  my $cluster_name = $config->{cluster}->{name};

  my $ecs = $self->get_ecs;

  if ( !$cluster_name ) {
    $cluster_name = $config->{app}->{name} . '-cluster';
    $config->{cluster}->{name} = $cluster_name;
  }

  if ( my $cluster_arn = $ecs->cluster_exists($cluster_name) ) {
    $self->get_logger->info( sprintf 'cluster: [%s] exists...skipping', $cluster_name );
    $self->inc_existing_resources( cluster => [$cluster_arn] );
  }
  else {
    $self->get_logger->info( sprintf 'cluster: [%s] will be created...%s', $cluster_name, $dryrun );
    $self->inc_required_resources(
      cluster => sub {
        my ($dryrun) = @_;
        return $dryrun ? "arn:???/$cluster_name" : $config->{cluster}->{arn};
      }
    );

    if ( !$dryrun ) {
      my $cluster = $ecs->create_cluster($cluster_name);
      $self->get_logger->info( sprintf 'cluster: [%s] created...', $cluster_name );
    }
  }

  return $TRUE;
}

########################################################################
sub add_ecr_policy {
########################################################################
  my ($self) = @_;

  my $tasks = $self->get_config->{tasks};

  my @repos = map { $tasks->{$_}->{image} =~ /^([^:]+):?.*?$/xsm } keys %{$tasks};

  for (@repos) {
    $_ = sprintf $ECR_ARN_TEMPLATE, $self->get_region, $self->get_account, $_;
  }

  return (
    { Effect   => 'Allow',
      Action   => ['ecr:GetAuthorizationToken'],
      Resource => q{*}
    },
    { Effect   => 'Allow',
      Action   => [qw(ecr:BatchGetImage ecr:GetDownloadUrlForLayer)],
      Resource => \@repos,
    }
  );
}

########################################################################
sub create_fargate_role { return create_role( @_, 'ecs-tasks' ); }
########################################################################

1;
