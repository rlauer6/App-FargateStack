package App::FargateStack::Builder::IAM;

use strict;
use warnings;

use Carp;
use Data::Dumper;
use Data::Compare;
use English qw(-no_match_vars);

use App::IAM;
use App::FargateStack::Constants;

use Role::Tiny;

########################################################################
sub build_iam_role {
########################################################################
  my ($self) = @_;

  my $config = $self->get_config;

  my $dryrun = $self->get_dryrun;

  my $tasks = $config->{tasks};

  my $iam = App::IAM->new( $self->get_global_options );

  my $default_name = $self->normalize_name( $config->{app}->{name} );

  my $role_name = $self->create_fargate_role( $iam, $default_name );

  my $policy_name = $config->{policy_name};
  $policy_name //= sprintf 'Fargate%sPolicy', $default_name;

  my $policy = $iam->get_role_policy( $role_name, $policy_name );

  my @statement;

  my $role_policy = {
    Version   => $IAM_POLICY_VERSION,
    Statement => \@statement,
  };

  push @statement, $self->add_ecr_policy();

  push @statement, $self->add_efs_policy();  # force rebuild of policy

  push @statement, $self->add_log_group_policy();

  if ( $config->{bucket} ) {
    push @statement, $self->add_bucket_policy;
  }

  if ( $config->{queue} ) {
    push @statement, $self->add_queue_policy();
  }

  if ( my $secrets = $self->get_secrets ) {
    push @statement, $self->add_secrets_policy($secrets);
  }

  if ($policy) {
    if ( Compare( $policy, $role_policy ) ) {
      $self->inc_existing_resources( 'iam:role-policy' => [$policy_name] );
      return;
    }

    $self->log_info( 'iam:policy [%s] will be replaced...%s', $policy_name, $dryrun );
  }

  $self->inc_required_resources( 'iam:policy' => [$policy_name] );

  return
    if $dryrun;

  if ($policy) {
    $self->log_info( 'iam:policy: deleting policy [%s] for role [%s]...', $policy_name, $role_name );
    $iam->delete_role_policy( $role_name, $policy_name );
  }

  $self->log_info( 'iam:policy: creating policy [%s] for role [%s]...', $policy_name, $role_name );

  $iam->put_role_policy( $role_name, $policy_name, $role_policy );

  croak sprintf "ERROR: could not create policy %s for %s\n%s", $role_name, $policy_name, $iam->get_error
    if $iam->get_error;

  return;
}

########################################################################
sub create_fargate_role { return create_role( @_, 'ecs-tasks' ); }
########################################################################

########################################################################
sub create_role {
########################################################################
  my ( $self, $iam, $default_name, $type ) = @_;

  my $config = $self->get_config;

  my $dryrun = $self->get_dryrun;

  my $trust_policy = {
    Version   => $IAM_POLICY_VERSION,
    Statement => [
      { Effect    => 'Allow',
        Principal => { Service => sprintf '%s.amazonaws.com', $type eq 'events' ? 'events' : 'ecs-task' },
        Action    => 'sts:AssumeRole'
      }
    ]
  };

  my $role_config;

  if ( $type eq 'events' ) {
    $config->{events_role} //= {};
    $role_config = $config->{events_role};
  }
  else {
    $config->{role} //= {};
    $role_config = $config->{role};
  }

  my $role_name = $role_config->{name};

  $role_name //= sprintf '%s%sRole', ( $type eq 'events' ? 'Events' : 'Fargate' ), $default_name;
  $role_config->{name} = $role_name;

  if ( my $role = $iam->role_exists($role_name) ) {
    $self->get_logger->info( sprintf 'iam: role [%s] exists...skipping', $role_name );
    $role_config->{arn} = $role->{Role}->{Arn};
    $self->inc_existing_resources( 'iam:role' => [ $role->{Role}->{Arn} ] );

    return $role_name;
  }

  $self->inc_required_resources(
    'iam:role' => [
      sub {
        my ($dryrun) = @_;
        return $dryrun ? "arn:???/$role_name" : $role_config->{arn};
      }
    ]
  );

  $self->get_logger->info( sprintf 'iam: [%s] will be created...%s', $role_name, $self->get_dryrun );

  if ( !$dryrun ) {
    $role_config->{arn} = $iam->create_role( $role_name, $trust_policy );

    croak sprintf "ERROR: could not create role: %s\n%s", $role_name, $iam->get_error
      if $iam->get_error;
  }

  return $role_name;
}

1;
