package App::ACM;

use strict;
use warnings;

use Carp;
use Data::Dumper;
use File::Temp qw(tempfile);
use JSON;

use Role::Tiny::With;
with 'App::AWS';

use parent 'App::Command';

__PACKAGE__->follow_best_practice();
__PACKAGE__->mk_accessors(qw(domain validation_method profile region));

########################################################################
sub delete_certificate {
########################################################################
  my ( $self, $cert_arn ) = @_;

  croak "usage: delete_certificate(certificate-arn)\n"
    if !$cert_arn;

  return $self->command( 'delete-certificate' => [ '--certificate-arn' => $cert_arn ] );
}

########################################################################
sub list_certificates {
########################################################################
  my ($self) = @_;

  return $self->command( 'list-certificates' => [ '--query' => 'CertificateSummaryList', ] );
}

########################################################################
sub describe_certificate {
########################################################################
  my ( $self, $cert_arn, $query ) = @_;

  croak "usage: descrirbe_certificate(certificate-arn, [query])\n"
    if !$cert_arn;

  return $self->command(
    'describe-certificate' => [
      '--certificate-arn' => $cert_arn,
      $query ? ( '--query' => $query ) : (),
    ]
  );
}

########################################################################
# returns the certificate arn
########################################################################
sub request_certificate {
########################################################################
  my ( $self, $domain, $validation_method ) = @_;

  $domain //= $self->get_domain;

  croak "usage: request_certificate(domain, [validation-method])\n"
    if !$domain;

  my $method = $validation_method // $self->get_validation_method // 'DNS';

  return $self->command(
    'request-certificate' => [
      '--domain-name'       => $domain,
      '--validation-method' => $method,
      '--output'            => 'text',
      '--query'             => 'CertificateArn',
    ]
  );
}

1;
